def artifactVersion() {
    def data = readFile('version')
    def major = data.findAll($/(?m)^STUDIO_MAJOR_VERSION=(\d+)/$).first().split('=').last()
    def minor = data.findAll($/(?m)^STUDIO_MINOR_VERSION=(\d+)/$).first().split('=').last()
    def patch = data.findAll($/(?m)^STUDIO_PATCH_LEVEL=(\d+)/$).first().split('=').last()
    return major + "." + minor + "." + patch
}

pipeline {
    agent none

    options {
        parallelsAlwaysFailFast()
        disableConcurrentBuilds()
    }

    stages {
        stage('Build') {
            parallel {
                stage ('linux') {
                    agent { label 'master' }
                    steps {
                        step([$class: 'WsCleanup'])
                        checkout scm
                        sh '''
                            qmake gams-studio.pro CONFIG+=release && make -j3
                        '''
                    }
                    post {
                        always {
                            recordIssues aggregatingResults: true, enabledForFailure: true, tools: [gcc4(reportEncoding: 'UTF-8')]
                            recordIssues enabledForFailure: true, tools: [taskScanner(ignoreCase: true, includePattern: '**/*.c, **/*.cpp, **/*.h, **/*.pro, **/*.pri', normalTags: 'TODO')]
                        }
                    }
                }
                stage ('windowsX64') {
                    agent { label 'windowsX64' }
                    steps {
                        step([$class: 'WsCleanup'])
                        checkout scm
                        bat '''
                            @echo off
                            echo Setting up environment for VS2017 usage...
                            cd "C:\\Program Files (x86)\\Microsoft Visual Studio\\2017\\Community\\VC\\Auxiliary\\Build"
                            call vcvars64.bat
                            cd %WORKSPACE%
                            qmake gams-studio.pro "CONFIG+=release"
                            nmake
                        '''
                    }
                    post {
                        always {
                            recordIssues aggregatingResults: true, enabledForFailure: true, tools: [msBuild(id: 'windowsX64', name: 'MSBuild-x64', reportEncoding: 'UTF-8')]
                        }
                    }
                }
                stage ('macos') {
                    agent { label 'macos' }
                    steps {
                        step([$class: 'WsCleanup'])
                        checkout scm
                        sh 'qmake gams-studio.pro CONFIG+=release && make -j3'
                    }
                    post {
                        always {
                            recordIssues aggregatingResults: true, enabledForFailure: true, tools: [clang(reportEncoding: 'UTF-8')]
                        }
                    }
                }
            }
        }
        stage('Test') {
            parallel {
                stage ('linux') {
                    agent { label 'master' }
                    steps {
                        sh '''
                            mkdir reports
                            cd tests/bin
                            for f in *; do
                                xvfb-run -a ./$f -xunitxml -o $WORKSPACE/reports/$f.xml
                            done
                        '''
                    }
                    post {
                        always {
                            junit allowEmptyResults: true, testResults: 'reports/*.xml'
                        }
                    }
                }
                stage ('windowsX64') {
                    agent { label 'windowsX64' }
                    steps {
                        bat '''
                            mkdir reports
                            cd tests/bin
                            for %%f in (*) do %%f -xunitxml -o %WORKSPACE%/reports/%%f.xml
                        '''
                    }
                    post {
                        always {
                            junit allowEmptyResults: true, testResults: 'reports/*.xml'
                        }
                    }
                }
                stage ('macos') {
                    agent { label 'macos' }
                    steps {
                        sh '''
                            mkdir reports
                            cd tests/bin
                            for f in *; do
                                ./$f -xunitxml -o $WORKSPACE/reports/$f.xml
                            done
                        '''
                    }
                    post {
                        always {
                            junit allowEmptyResults: true, testResults: 'reports/*.xml'
                        }
                    }
                }
            }
        }
        stage('Pack') {
            parallel {
                stage ('linux') {
                    agent { label 'master' }
                    steps {
                        sh '''
                            # create folder structure
                            mkdir -p appdir/usr/bin
                            mkdir -p appdir/usr/share/pixmaps
                            mkdir -p appdir/usr/resources

                            # collect files
                            cp src/bin/studio appdir/usr/bin/
                            cp platform/linux/gamsstudio.desktop appdir/usr/share/studio.desktop
                            cp icons/* appdir/usr/share/pixmaps
                            cp CHANGELOG appdir/usr/resources/Changelog

                            STUDIO_MAJOR_VERSION=$(grep ^STUDIO_MAJOR_VERSION version | cut -f2 -d"=")
                            STUDIO_MINOR_VERSION=$(grep ^STUDIO_MINOR_VERSION version | cut -f2 -d"=")
                            STUDIO_PATCH_LEVEL=$(grep ^STUDIO_PATCH_LEVEL version | cut -f2 -d"=")
                            export VERSION=$STUDIO_MAJOR_VERSION.$STUDIO_MINOR_VERSION.$STUDIO_PATCH_LEVEL

                            # Chromium crash workaround
                            mkdir -p appdir/usr/lib
                            cp /usr/lib/x86_64-linux-gnu/nss/* ./appdir/usr/lib

                            linuxdeployqt-6-x86_64.AppImage appdir/usr/share/studio.desktop -appimage -bundle-non-qt-libs -no-translations -extra-plugins=iconengines
                        '''
                    }
                }
                stage ('windowsX64') {
                    agent { label 'windowsX64' }
                    steps {
                        bat '''
                            windeployqt --release --compiler-runtime --force src\\bin\\studio.exe

                            copy CHANGELOG "src\\bin\\resources\\Changelog"
                            copy "C:\\Program Files (x86)\\Microsoft Visual Studio\\2017\\Community\\VC\\Redist\\MSVC\\14.12.25810\\x64\\Microsoft.VC141.CRT\\*" "src\\bin"
                            copy "C:\\Program Files (x86)\\Microsoft Visual Studio\\2017\\Community\\VC\\Redist\\MSVC\\14.12.25810\\vc_redist.x64.exe" "src\\bin"
                        '''
                        fileOperations([folderCreateOperation('tmp/studio'), folderCopyOperation(destinationFolderPath: 'tmp/studio', sourceFolderPath: 'src/bin')])
                        withCredentials([certificate(aliasVariable: '', credentialsId: '302df092-b329-47b9-8f7a-605da075126d', keystoreVariable: 'WIN_CERT_FILE', passwordVariable: 'WIN_CERT_PASSWD')]) {
                            bat '''
                                echo Setting up environment for VS2017 usage...
                                cd "C:\\Program Files (x86)\\Microsoft Visual Studio\\2017\\Community\\VC\\Auxiliary\\Build"
                                call vcvars64.bat
                                cd %WORKSPACE%\\tmp\\studio
                                signtool sign /v /f %WIN_CERT_FILE% /p %WIN_CERT_PASSWD% /tr http://timestamp.comodoca.com studio.exe
                            '''
                        }
                    }
                }
                stage ('macos') {
                    agent { label 'macos' }
                    steps {
                        fileOperations([folderRenameOperation(destination: 'src/bin/GAMS Studio.app', source: 'src/bin/studio.app')])
                        withCredentials([string(credentialsId: '2e590f95-5e30-4101-93be-31a1b7402da4', variable: 'MACOS_KEYCHAIN_PASSWD')]) {
                            sh '''
                                cd src/bin
                                security unlock-keychain -p ${MACOS_KEYCHAIN_PASSWD}

                                macdeployqt "GAMS Studio.app" -always-overwrite
                                cp ../../CHANGELOG "GAMS Studio.app/Contents/Resources/Changelog"
                                codesign --sign ${CODESIGN_IDENTITY} --options runtime --force --deep --timestamp --entitlements ../../platform/macos/studio.entitlements.plist "GAMS Studio.app"
                                codesign --sign ${CODESIGN_IDENTITY} --options runtime --force --timestamp --entitlements ../../platform/macos/webengine.entitlements.plist "GAMS Studio.app/Contents/Frameworks/QtWebEngineCore.framework/Helpers/QtWebEngineProcess.app/Contents/MacOS/QtWebEngineProcess"
                                hdiutil create -volname "GAMS Studio" -srcfolder "GAMS Studio.app" -ov -fs HFS+ -format UDZO "GAMS Studio.dmg"
                            '''
                            sh '''
                                codesign --verify --verbose "src/bin/GAMS Studio.app"
                            '''
                        }
                        sh '''
                            mv "src/bin/GAMS Studio.dmg" studio.dmg
                        '''
                        script {
                            def versionData = artifactVersion()
                            if (env.BRANCH_NAME != 'master') {
                                sh "mv studio.dmg ${env.BRANCH_NAME}-studio-${versionData}-x86_64.dmg"
                            } else {
                                sh "mv studio.dmg GAMS_Studio-${versionData}-x86_64.dmg"
                            }
                        }
                    }
                }
            }
        }
        stage('Notarize') {
            agent { label 'macos' }
            when {
                expression { return env.BRANCH_NAME == "master" || env.BRANCH_NAME ==~ /.*-rc/}
            }
            steps {
                withCredentials([usernamePassword(credentialsId: '4568bda5-894f-4fa1-b9c5-a8d42f6e61ac', passwordVariable: 'APPLE_ID_PASS', usernameVariable: 'APPLE_ID_USER')]) {
                    sh '''
                        DMG_NAME=$(find *.dmg)

                        xcrun altool --notarize-app -f $DMG_NAME --primary-bundle-id com.gams.studio    \
                                     -u ${APPLE_ID_USER} -p ${APPLE_ID_PASS} &> upload.log || true

                        REQUEST_UUID=$(cat upload.log | grep RequestUUID | cut -f2 -d"=" | tr -d '[:space:]')
                        if [ -z "$REQUEST_UUID"];
                        then
                            REQUEST_UUID=$(cat upload.log | grep -e "\\*\\*\\* Error: ERROR ITMS-90732: .*" |   \
                                           sed -nE "s/^[[:digit:]]+.*ID is ([0-9a-z-]+)\\".*/\\1/p")
                        fi

                        while true; do
                            sleep 60s

                            xcrun altool --notarization-info $REQUEST_UUID --output-format xml  \
                                         -u ${APPLE_ID_USER} -p ${APPLE_ID_PASS} &> report.xml

                            NOTARIZATION_STATUS=$(/usr/libexec/PlistBuddy -c "Print :notarization-info:Status" report.xml)
                            if [ $? -eq 0 ]; then
                                if [ "$NOTARIZATION_STATUS" != "in progress" ]; then
                                    echo "Notarization ended; result: $NOTARIZATION_STATUS"
                                    break
                                fi
                                echo "Notarization still in progress. Waiting 60s to check again."
                            else
                                echo "Could not determine notarization status; giving up. Server response:"
                                cat report.xml
                                exit 1
                            fi
                        done

                        # Staple the notarization ticket to the app bundle
                        if [ "$NOTARIZATION_STATUS" == "success" ]; then
                            echo "Notarization successful; stapling ticket to app bundle"
                            xcrun stapler staple $DMG_NAME
                        else
                            echo "Notarization failed."
                            exit 1
                        fi
                    '''
                }
            }
            post {
                always {
                    archiveArtifacts artifacts: 'upload.log, report.xml', fingerprint: true
                }
            }
        }
        stage('Archive') {
            parallel {
                stage('linux') {
                    agent { label 'master'}
                    steps {
                        script {
                            def versionData = artifactVersion()
                            if (env.BRANCH_NAME != 'master') {
                                sh "mv GAMS_Studio-*.AppImage ${env.BRANCH_NAME}-studio-${versionData}-x86_64.AppImage"
                                archiveArtifacts artifacts: '*studio*.AppImage', fingerprint: true
                            } else {
                                archiveArtifacts artifacts: '*.AppImage', fingerprint: true
                            }
                        }
                    }
                }
                stage('windowsX64') {
                    agent { label 'windowsX64' }
                    steps {
                        script {
                            def versionData = artifactVersion()
                            if (env.BRANCH_NAME != 'master') {
                                zip zipFile: "${env.BRANCH_NAME}-studio-${versionData}-x86_64.zip", archive: true, dir: 'tmp'
                            } else {
                                zip zipFile: "GAMS_Studio-${versionData}-x86_64.zip", archive: true, dir: 'tmp'
                            }
                        }
                    }
                }
                stage('macos') {
                    agent { label 'macos' }
                    steps {
                        archiveArtifacts artifacts: '*.dmg', fingerprint: true
                    }
                }
            }
        }
    }
}
